import com.github.gradle.node.npm.task.NpmTask

plugins {
    id 'org.apache.grails.gradle.grails-web'
    id 'org.apache.grails.gradle.grails-gsp'
    id 'com.github.node-gradle.node'
    id 'com.gorylenko.gradle-git-properties'
    id 'war'
}

group = 'io.github.matrei'
version = projectVersion

// This is used for testing with local copy of the Grails Inertia Plugin
// (comment out the dependency in the dependencies block)
// Also see section in settings.gradle
/*
grails {
    plugins {
        implementation project(':grails-inertia')
    }
}
*/

repositories {
    mavenCentral()
    maven { url = 'https://repo.grails.org/grails/restricted' }
    maven {
        url = 'https://repository.apache.org/content/repositories/staging'
        content {
            includeGroupAndSubgroups('org.apache.grails')
            includeGroupAndSubgroups('org.apache.groovy')
        }
        mavenContent {
            releasesOnly()
        }
    }
    maven {
        url = 'https://repository.apache.org/content/repositories/snapshots'
        content {
            includeGroupAndSubgroups('org.apache.grails')
            includeGroupAndSubgroups('org.apache.groovy')
        }
        mavenContent {
            snapshotsOnly()
        }
    }
}

dependencies {

    implementation platform("org.apache.grails:grails-bom:$grailsVersion")

    // Grails Core Modules
    implementation 'org.apache.grails:grails-core'
    implementation 'org.apache.grails:grails-controllers'
    implementation 'org.apache.grails:grails-data-hibernate5'
    implementation 'org.apache.grails:grails-events'
    implementation 'org.apache.grails:grails-gsp'
    implementation 'org.apache.grails:grails-layout'
    implementation 'org.apache.grails:grails-interceptors'
    implementation 'org.apache.grails:grails-views-gson'
    implementation 'org.apache.grails.web:grails-web-common'
    implementation 'org.apache.grails.web:grails-web-databinding'

    // Grails Plugins
    implementation "org.apache.grails:grails-spring-security:$grailsSpringSecurityVersion"
    implementation "org.grails.plugins:grails-logical-delete:$grailsLogicalDeleteVersion"
    implementation "io.github.matrei:grails-inertia:$grailsInertiaVersion"

    //implementation libs.grailsEvents
    //implementation project(':grails-inertia')

    //implementation libs.bundles.springSecurity

    implementation "commons-io:commons-io:$commonsIoVersion"
    implementation "net.datafaker:datafaker:$datafakerVersion"
    implementation "org.imgscalr:imgscalr-lib:$imgscalrVersion"
    implementation "com.drewnoakes:metadata-extractor:$metadataExtractorVersion"
    implementation "dev.nicklasw:squiggly-filter-jackson:$squigglyFilterVersion"

    runtimeOnly 'org.apache.grails:grails-services'
    runtimeOnly 'org.apache.grails:grails-url-mappings'

    runtimeOnly "io.github.matrei:grails-csrf:$grailsCsrfVersion"
    runtimeOnly 'org.fusesource.jansi:jansi'
    runtimeOnly 'org.springframework.boot:spring-boot-autoconfigure'
    runtimeOnly 'org.springframework.boot:spring-boot-starter-logging'
    runtimeOnly 'org.springframework.boot:spring-boot-starter-tomcat'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.zaxxer:HikariCP'

    compileOnly 'org.slf4j:slf4j-nop' // Get rid of warning about missing slf4j implementation during GSP compile tasks

    testImplementation 'org.apache.grails:grails-testing-support-datamapping'
    testImplementation 'org.apache.grails:grails-testing-support-web'
    testImplementation 'org.spockframework:spock-core'
    testImplementation 'org.apache.groovy:groovy-test'

    integrationTestImplementation "io.micronaut:micronaut-http-client:$micronautVersion"
    integrationTestImplementation "io.micronaut:micronaut-jackson-databind:$micronautVersion"
    integrationTestImplementation 'org.seleniumhq.selenium:selenium-firefox-driver'
    integrationTestImplementation testFixtures('org.apache.grails:grails-geb')

    console 'org.apache.grails:grails-console'
}

compileJava.options.release = javaVersion.toInteger()

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    testLogging {
        events('passed', 'skipped', 'failed', 'standardOut', 'standardError')
    }
    reports.html.required = false
    reports.junitXml.required = false

    systemProperty('grails.geb.atCheckWaiting.enabled', true)
    systemProperty('grails.geb.recording.mode', 'RECORD_FAILING')
/*
    jvmArgs += [
            '-Xmx2g', '-Xdebug', '-Xnoagent', '-Djava.compiler=NONE',
            '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005'
    ]
*/
}

node {
    download = true
}

tasks.register('viteBuild', NpmTask) {
    group = 'build'
    description = 'Build the client bundle'

    inputs.dir(layout.projectDirectory.dir('node_modules'))
    inputs.dir(layout.projectDirectory.dir('src/main/javascript'))
    inputs.files(layout.projectDirectory.files('package.json', 'vite.config.js', 'tailwind.config.js', 'postcss.config.js', 'babel.config.js', 'jsconfig.json'))

    outputs.dir(layout.projectDirectory.dir('src/main/resources/public/dist'))

    it.args = ['run', 'build']

    dependsOn('npmInstall')
}

tasks.register('copyNodeModulesForSsr', Copy) {
    group = 'build'
    description = 'Copy node_modules for SSR'

    from(layout.projectDirectory.dir('node_modules'))
    into(layout.buildDirectory.dir('resources/main/node_modules'))

    dependsOn('npmInstall')
}

tasks.named('processResources') {
    dependsOn('viteBuild', 'copyNodeModulesForSsr')
}

/*
tasks.named('bootRun', JavaExec) {
    jvmArgs += [
            '-Xmx2g', '-Xdebug', '-Xnoagent', '-Djava.compiler=NONE',
            '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005'
    ]
}
*/
